//TerminalQuest-InsaneGame
programa
{
	inclua biblioteca Matematica --> mat
	inclua biblioteca Texto --> txt
	inclua biblioteca Util --> u
	
	cadeia matriz[8][12], direcao = "d", itens[30], pos_caixa, GLOBAL_opcao, GLOBAL_inventario[5][6], GLOBAL_arquivos_ambiente[7], arma = "0"
	cadeia habilidade_inimigo = ""
	inteiro integridade_inimigo = 100, bits_inimigo = integridade_inimigo, dano_inimigo = 0, valor_kernels
	
	cadeia texto_daemon[7] = {
		
		"     ░▓▓░    ░▓▓░      ",
		"     ░▓▓▓▓▓▓▓▓▓▓░      ",
		"     ░▓▓▓▓▓▓▓▓▓▓░      ",
		"    ░▓▓▓▓▓▓▓▓▓▓▓▓░     ",
		"    ░▓▓▓▓▓▓▓▓▓▓▓▓░     ",
		"      ░▒▓▓▓▓▓▓▒░       ",
		"        ░▓▓▓▓░         "
	}
	
	cadeia texto_jogador[7] = {
		
	    "        ▒▒▒▒▒▒▒        ",
	    "      ░▓░░░  ░ ▓░      ",
	    "      █▒░░█  █░ ▓      ",
	    "      ██▒░░░░░░██      ",
	    "       ██▓▓▓▓▓▓█       ",
	    "       ██▓███▓▓█       ",
	    "       ▓███ ▓███       "
	}
	
	cadeia texto_desconhecido[7] = {
		
		"       ████████        ",
		"      ████  ████       ",
		"          ██████       ",
		"        ███████        ",
		"                       ",
		"         ████          ",
		"         ████          "
	}

	cadeia texto_virus[7] = {
		                    
	"        π   √≠√        ",
	"      π∞≈π∞∞π≈π∞ππ     ",
	"     √≈π÷÷÷÷÷=ππ≈≈π    ",
	"    ∞≠ππ÷÷÷÷÷×÷÷πππ    ",
	"    π≈≠ππ=÷÷÷÷÷π≈√     ",
	"      π ∞π≈π∞∞π≈∞π     ",
	"         √≠√   ππ      "
	}

                    
	inteiro y = 4, x = 2, fase = 0, integridade = 100, bits = integridade
	inteiro x_chave, y_chave, n_itens = 0
	inteiro  possui_chave[5] = {0,0,0,0,0}
	real versao = 1.0, kernel = 0.0
	real dano = 0.0
	logico parou_por_algum_motivo = falso, porta_saida[5] = {falso, falso, falso,falso, falso}, porta_entrada = falso, pular_dialogo, inimigoMorto[8][12], chefeMorto = falso
	logico luta_chefe = falso
	


	funcao inicio(){

				
		para(inteiro i = 0; i<8; i++){
			para(inteiro j = 0; j<12; j++){
				inimigoMorto[i][j] = falso
			}
		}
		para(inteiro i = 0; i < 5; i++){
			para(inteiro j = 0; j < 6; j++){
				se(i != 0 ou j != 0 e j != 1){
					GLOBAL_inventario[i][j] = "."
				}senao se(j == 0){
					GLOBAL_inventario[i][j] = "0"
					n_itens++
					itens[(i * 6) + j] = "0"
				}senao se(j == 1){
					GLOBAL_inventario[i][j] = "1"
					n_itens++
					itens[(i * 6) + j] = "1"
				}
			}
		}

		cadeia comandos
		pular_dialogo = terminalQuest()

		se(nao pular_dialogo){
			fala_inicial()
		}

		abrir_terminal()
	}
	
	funcao fala_inicial(){
		falas("- ... Hummm, o que?^      Onde estou?", 76, "jogador")
		falas("- Que dor de cabeça^//jogador se levanta^  Mas o quê é isso???^//diz olhando ao horizonte", 76, "jogador")
		falas("- \"Isso\"? Você fala como se eu fosse uma    coisa^//diz um ser se aproximando ao longe", 76, "desconhecido")
		falas("- Eu sou um Daemon, um processo abandonado^  sem um PID 1...", 76, "daemon")
		falas("- Ok, mas o que é este mundo?", 76, "jogador")
		falas("- Não. A pergunta certa é: em qual Versão dele você acordou?", 76, "daemon")
		falas("- Nós chamamos esta terra de terminal", 76, "daemon")
		falas("- Espera, tipo o terminal do computador?", 76, "jogador")
		falas("- Exatamente", 76, "daemon")
		falas("- Ok... Mas o que é aquilo?^//diz o jogador olhando ao longe uma gigantesca montanha", 76, "jogador")
		falas("- Isso você descobrirá em breve...", 76, "daemon")
	}

	funcao falas(cadeia texto, inteiro velocidade, cadeia locutor){

		cadeia texto_exibido[9] = {"", "", "", "", "", "", "", "", ""}, passar_dialogo, retrato[7]
		inteiro numero_caracteres = txt.numero_caracteres(texto), linha = 2, limite_linha = 40

		para(inteiro i = 0; i < 7; i++){
			se(locutor == "daemon"){
				retrato[i] = texto_daemon[i]
			}senao se(locutor == "jogador"){
				retrato[i] = texto_jogador[i]
			}senao se(locutor == "desconhecido"){
				retrato[i] = texto_desconhecido[i]
			}senao{
				retrato[i] = texto_virus[i]
			}
			
		}

		para(inteiro i = 0; i < numero_caracteres; i++){

			caracter c = txt.obter_caracter(texto, i)

				se(c != '^'){
					texto_exibido[linha] += c
				}

			escreva("┌───────────────────────┐\n")
			se((txt.numero_caracteres(texto_exibido[linha]) >= limite_linha e c == ' ') ou c == '^'){
					linha++
			}
			
			para(inteiro j = 0; j < 7; j++){
				se(c != '^'){
					escreva("│", retrato[j], "│     ", texto_exibido[j], "\n")
				}
			}
			escreva("└───────────────────────┘")
			se(i == numero_caracteres - 1){
				escreva("     PRESSIONE ENTER PARA CONTINUAR: ")
				leia(passar_dialogo)
				se(passar_dialogo == "1"){
					pare
				}
			}senao{
				u.aguarde(velocidade)
			}
			limpa()
		}
	}

	funcao define_caractere(){
		para(inteiro i = 0; i < 8; i++){
			para(inteiro j = 0; j < 12; j++){
				//borda
				se(y == i e x == j){
					matriz[i][j] = "#"
				}
				
				senao se(j == 11 ou j == 0){
					matriz[i][j] = "|"
				}
				senao se(i == 7 ou i == 0){
					matriz[i][j] = "—"
				

					
				
				//fase0
				}senao se(fase == 0 e i == 1 e j == 7 e possui_chave[fase] == 0){
					se(possui_chave[fase] == 0){
						matriz[i][j] = "+"
						x_chave = j
						y_chave = i
				}senao{
					matriz[i][j] = "."
				}
				
					//fase1
				}senao se(fase == 1){
					se(possui_chave[fase] == 0 e i == 4 e j == 7){
						matriz[i][j] = "+"
						x_chave = j
						y_chave = i
					}senao se((i > 1 e i < 6) e j == 6){
						matriz[i][j] = "|"
					}senao{
						matriz[i][j] = "."
					}
				}

					//fase2
				senao se(fase == 2 e (j == 7 ou (i == 4 e j == 4))){
					se(i != 4){
						matriz[i][j] = "|"
					}senao se(i == 4 e j == 7){
						se(nao inimigoMorto[i][j]){
							matriz[i][j] = "$"
						}senao{
							matriz[i][j] = "."
						}
					}senao se(possui_chave[fase] == 0){
						matriz[i][j] = "+"
						x_chave = j
						y_chave = i
					}senao{
						matriz[i][j] = "."
					}
				}
				
						//fase3Montanha

				senao se(fase == 3 e (j > 2 e j < 10) e (i != 4 e i != 3)){
					se(j == 3 ou j == 5 ou j == 7 ou j == 9){
							matriz[i][j] = "|"
						}
					senao se(i == 5 ou i == 2){
						se(nao inimigoMorto[i][j]){
							matriz[i][j] = "$"
						}senao{
							matriz[i][j] = "."
						}
					}senao se(possui_chave[fase] != 1){
						matriz[i][j] = "+"
						x_chave = 6
						y_chave = 1
					}senao{
						matriz[i][j] = "."
						}

				//espaco vazio
				}
				
				senao se(fase == 4 e (j == 4 e i == 4)){
					se(possui_chave[fase] != 1){
						matriz[i][j] = "+"
						x_chave = j
						y_chave = i
					}senao{
						se(j == 4 e i==4){
							matriz[i][j] = "."
						}
						se(nao chefeMorto e x == 6){
							luta_chefe = verdadeiro
							chameChefe()
							combate(verdadeiro, verdadeiro)
						}
					}
					//espaco vazio
				}senao{
					matriz[i][j] = "."
				}
			}
		}
			se((possui_chave[fase] == 1)){
				matriz[3][11] = "\\"
				matriz[4][11] = "/"
				porta_saida[fase] = verdadeiro
			}
			se((fase > 0)){
				matriz[3][0] = "\\"
				matriz[4][0] = "/"
				porta_entrada = verdadeiro
			}
	}

	funcao desenha_matriz(){
		limpa()
		escreva("operator@kernel:/world/sector_0", fase, "$ cat map.txt\n\nsector_0", fase, ".map  [kernel ", versao, "]\n\n")
		para(inteiro i = 0; i < 8; i++){
			escreva("   ")
		para(inteiro j = 0; j < 12; j++){
			escreva(matriz[i][j], "  ")
		}
			escreva("\n")
		}
	}

	funcao movimentacao(){
  		inteiro chanceInimigo = 0
		escreva("\ncwd: /world/sector_0", fase, "\npos: (", x, ",", y, ")\ninput: ")
		leia(direcao)
	  
		se(direcao == "d" e (matriz[y][x + 1] != "|" e matriz[y][x + 1] != "—")){
			se(possui_chave[fase] == 1 e (y == 3 ou y == 4) e x == 10){
				fase++
				x = 2
			}senao{
				x++
			}
		}senao se(direcao == "a" e (matriz[y][x - 1] != "|"  e matriz[y][x - 1] != "—")){
			
			se(porta_entrada e (y == 3 ou y == 4) e x == 1){
				fase--
				x = 10
			}senao{
				x--
			}
			
		}senao se(direcao == "w" e (matriz[y - 1][x] != "|"  e matriz[y - 1][x] != "—")){
		
			y--
						
		}senao se((matriz[y + 1][x] != "|"  e matriz[y + 1][x] != "—") e direcao == "s" e y < 6){
			
			y++
			
		}senao se(direcao == "1"){
			fase++
			
		}senao se(direcao == "2"){
			fase--
			
		}senao se(direcao == "^c"){
			abrir_terminal()
			parou_por_algum_motivo = verdadeiro
		}
		se((x == x_chave e y == y_chave) ou direcao == "21"){
			possui_chave[fase] = 1
			adicionar_item("gate_0" + fase + ".key")
			
		}senao se(matriz[y][x] == "$"){
					combate(verdadeiro, falso)
		}
	}

	funcao escreva_lento(cadeia texto, inteiro velocidade){
		inteiro passar_dialogo
		inteiro numero_caracteres = txt.numero_caracteres(texto)

		para(inteiro i = 0; i < numero_caracteres; i++){
			escreva(txt.obter_caracter(texto, i))
			u.aguarde(velocidade)
		}
	}
	
	funcao logico terminalQuest(){

		logico pular_dialogo = falso
		cadeia continuar
		
		escrevaTerminal()
			u.aguarde(1000)
			limpa()
		escrevaQuest(1)
			u.aguarde(1000)
			limpa()
		escrevaTerminal()
		escrevaQuest(2)
		escreva("PRESSIONE ENTER PARA CONTINUAR: ")
		leia(continuar)
		se(continuar == "1"){pular_dialogo = verdadeiro}
		limpa()

		retorne pular_dialogo
	}
	
	funcao menu(){
		escreva("\n/===========================================\\ \n")
		escreva("||                                         ||     COMANDOS SUPORTADOS:\n")
		escreva("||                                         ||\n")
		escreva("||                                         ||\n")
		escreva("||                                         ||     > ./map.sh;\n")
		escreva("||   01101101  01100101 01101110 01110101  ||\n")
		escreva("||    .-.-.-.   .---.   .-..-.   .-..-.    ||     > ls -l inv;\n")
		escreva("||    | | | |   | |-    | .` |   | || |    ||\n")
		escreva("||    `-'-'-'   `---'   `-'`-'   `----'    ||     > ls -l;\n")
		escreva("||                                         ||\n")
		escreva("||                                         ||     > ./help.sh;\n")
		escreva("||                                         ||\n")
		escreva("||                                         ||     > ./logout.sh;\n")
		escreva("\\===========================================/\n")

		abrir_terminal()
	}

	funcao escrevaTerminal(){
		escreva("  _________  _______   ________  _____ ______   ___  ________   ________  ___          \n",
		        " |\\___   ___\\\\  ___ \\ |\\   __  \\|\\   _ \\  _   \\|\\  \\|\\   ___  \\|\\   __  \\|\\  \\         \n",
		        " \\|___\\  \\_\\ \\   __/|\\ \\  \\|  \\ \\  \\  \\\\__\\\\  \\  \\  \\  \\\\ \\  \\  \\  \\|  \\ \\ \\  \\      \n",
		        "     \\ \\  \\ \\ \\  \\_|/_\\ \\   _  _\\ \\  \\\\|__| \\  \\  \\  \\  \\\\ \\  \\  \\   __  \\ \\  \\       \n",
		        "      \\ \\  \\ \\ \\  \\_|\\ \\ \\  \\\\  \\\\ \\  \\    \\ \\  \\  \\  \\  \\\\ \\  \\  \\  \\ \\  \\ \\  \\____  \n",
		        "       \\ \\__\\ \\ \\_______\\ \\__\\\\ _\\\\ \\__\\    \\ \\__\\ \\__\\ \\__\\\\ \\__\\ \\__\\ \\__\\ \\_______\\ \n",
		        "        \\|__|  \\|_______|\\|__|\\|__|\\|__|     \\|__|\\|__|\\|__| \\|__|\\|__|\\|__|\\|_______|\n")
	}

	funcao escrevaQuest(inteiro chamada){
		se(chamada == 1){escreva("\n\n\n\n\n\n\n")}
	   escreva(  " ________  ___  ___  _______   ________  _________                                    \n",
		        "|\\   __  \\|\\  \\|\\  \\|\\  ___ \\ |\\   ____\\|\\___   ___\\                                  \n",
		        "\\ \\  \\|\\  \\ \\  \\\\  \\ \\   __/|\\ \\  \\___|\\|___ \\  \\_|                                  \n",
		        " \\ \\  \\\\  \\ \\  \\\\  \\ \\  \\_|/_\\ \\_____  \\   \\ \\   \\                                    \n",
		        "  \\ \\  \\\\  \\ \\  \\\\  \\ \\  \\_|\\ \\|____|\\  \\   \\ \\   \\                                  \n",
		        "   \\ \\_____  \\ \\_______\\ \\_______\\____\\_\\  \\   \\ \\__\\                                 \n",
		        "    \\|___| \\__\\|_______|\\|_______|\\_________\\   \\|__|                                 \n",
		        "          \\|__|                  \\|_________|                                           ")
	}
		
	funcao chameJogo(){
		define_caractere()
		enquanto(nao parou_por_algum_motivo){
			desenha_matriz()
			movimentacao()
			define_caractere()
		}
	}

	funcao help(){
		escreva("┌─────────────────────────────────────────────────────────────┐\n")
		escreva("│                         HELP                                │\n")
		escreva("├─────────────────────────────────────────────────────────────┤\n")
		escreva("│                                                             │\n")
		escreva("│  ./map.sh      - Comece a jogar                             │\n")
		escreva("│  ls -l inv     - Olhar inventario                           │\n")
		escreva("│  ls -l         - Olhe o ambiente                            │\n")
		escreva("│  ./logout      - Sair do jogo                               │\n")
		escreva("│                                                             │\n")
		escreva("├─────────────────────────────────────────────────────────────┤\n")
		escreva("│                      COMO JOGAR                             │\n")
		escreva("│                                                             │\n")
		escreva("│  wasd          - Movimentação                               │\n")
		escreva("│  ENTER         - Confirmar cada tecla                       │\n")
		escreva("│  (+)           - Coletar chaves                             │\n")
		escreva("│  \"Break\"       - Voltar terminal de comandos                │\n")
		escreva("│                                                             │\n")
		escreva("│  Colete as chaves para abrir as portas e passar de nivel.   │\n")
		escreva("│                                                             │\n")
		escreva("└─────────────────────────────────────────────────────────────┘\n")
		abrir_terminal()
	}

	funcao mostrar_inventario(logico abrirTerminal){

		cadeia continuar
		
		escreva("\n\ntotal ", n_itens, "\n\n")
		
		para(inteiro i = 0; i < 5; i++){
			para(inteiro j = 0; j < 6; j++){
				se(GLOBAL_inventario[i][j] != "."){
					escreva("-rw------- daemon root 1.0K ", GLOBAL_inventario[i][j], "\n")
				}
			}
		}
		se(abrirTerminal){
		abrir_terminal()
		}senao{
			escreva("Press enter to continue: ")
			leia(continuar)
		}
	}

	funcao adicionar_item(cadeia item){
		para(inteiro i = 0; i < 5; i++){
			para(inteiro j = 0; j < 6; j++){
				se(GLOBAL_inventario[i][j] == "."){
					GLOBAL_inventario[i][j] = item
					n_itens++
					retorne
				}
			}
		}
	}

	funcao abrir_terminal(){
		
		cadeia comando
		
		escreva("\noperator@kernel:~$: ")
		leia(comando)
		
		se(comando == "./map.sh")			{chameJogo()}
		senao
		se(comando == "ls -l inv")		{mostrar_inventario(verdadeiro)}
		senao
		se(comando == "ls -l")				{listar_ambiente()}
		senao 
		se(comando == "./logout.sh"){
			escreva("\nlogout")
			u.aguarde(200)
			limpa()
			escreva("\noperator@kernel:~$: logout\n\nlogout.")
			u.aguarde(200)
			limpa()
			escreva("\noperator@kernel:~$: logout\n\nlogout..")
			u.aguarde(200)
			limpa()
			escreva("\noperator@kernel:~$: logout\n\nlogout...")
			u.aguarde(200)
			limpa()
			escreva("\noperator@kernel:~$: logout\n\nlogout")
			u.aguarde(200)
			limpa()
			escreva("\noperator@kernel:~$: logout\n\nlogout\n\nNo active world found outside this session.\n")
			abrir_terminal()
			
		}
		senao
		se(comando == "./help.sh")			{help()}
		senao
		se(comando == "cat readme.txt")	{menu()}
		senao
		se(comando == "clear")			{limpa() abrir_terminal()}
		senao{						escreva("\nbash: command not found\nCurrent directory: /home/operator\nHint: use 'cat readme.txt' to open the menu.\n") abrir_terminal()}
	}

	funcao listar_ambiente(){

		inteiro n_arquivos = 0
		cadeia arquivos_ambiente[7]
		logico chave_listada = falso
		logico rocha_listada = falso

		//Verifica se os valores referentes a chave e a rocha já foram atribuidos ao vetor "arquivos_ambiente"

		para(inteiro i = 0; i < 7; i++){
				se(arquivos_ambiente[i] == "-rw------- daemon root 256B gate_0" + fase + ".key"){
					chave_listada = verdadeiro
				}senao se(arquivos_ambiente[i] == "-rw-r--r-- daemon root 4.0K rock.dat"){
					rocha_listada = verdadeiro
				}
			}

		//atribui o valor "gate_0(fase).key" ao vetor "arquivos_ambiente" caso as condições dos comandos "se" sejam verdadeiras
		
		se(possui_chave[fase] != 1 e nao chave_listada){
			para(inteiro i = 0; i < 7; i++){
				se(arquivos_ambiente[i] == ""){
					arquivos_ambiente[i] = "-rw------- daemon root 256B gate_0" + fase + ".key"
					n_arquivos++
					pare
				}
			}
		}

		//atribui o valor "rock.dat" ao vetor "arquivos_ambiente" caso as condições dos comandos "se" sejam verdadeiras
		
		se(fase == 1 e nao rocha_listada){
			para(inteiro i = 0; i < 7; i++){
				se(arquivos_ambiente[i] == ""){
					arquivos_ambiente[i] = "-rw-r--r-- daemon root 4.0K rock.dat"
					n_arquivos++
					pare
				}
			}
		}

		para(inteiro i = 0; i < 7; i++){
			GLOBAL_arquivos_ambiente[i] = arquivos_ambiente[i]
		}

		//escreve os itens que corresponde a listagem de arquivos no ambiente
		
		escreva("\ntotal ", n_arquivos, "\n")
		para(inteiro i = 0; i < 7; i++){
			se(arquivos_ambiente[i] != ""){
				escreva("\n", arquivos_ambiente[i], "\n")
			}
		}
		abrir_terminal()
	}

	funcao inteiro rodar_dado(cadeia quem){
		inteiro numero_sorteado = u.sorteia(1, 20)
		cadeia numero_mostrado
		se(numero_sorteado < 10){
			numero_mostrado = "0" + numero_sorteado
		}senao{
			numero_mostrado = numero_sorteado + ""
		}

		limpa()

		u.aguarde(500)

		limpa()
		escreva("O número sorteado do ", quem, " é.\n\n┌──────────┐\n")
		escreva("│          │\n")
		escreva("│    04    │\n")
		escreva("│          │\n")
		escreva("└──────────┘")

		u.aguarde(500)

		limpa()
		escreva("O número sorteado do ", quem, " é..\n\n┌──────────┐\n")
		escreva("│          │\n")
		escreva("│    17    │\n")
		escreva("│          │\n")
		escreva("└──────────┘")

		u.aguarde(500)

		limpa()
		escreva("O número sorteado do ", quem, " é...\n\n┌──────────┐\n")
		escreva("│          │\n")
		escreva("│    09    │\n")
		escreva("│          │\n")
		escreva("└──────────┘")

		u.aguarde(500)
		limpa()

		para(inteiro i = 0; i < 4; i++){

			escreva("O número sorteado do ", quem, " é:\n\n┌──────────┐\n")
			escreva("│          │\n")
			escreva("│    ", numero_mostrado, "    │\n")
			escreva("│          │\n")
			escreva("└──────────┘")
			u.aguarde(500)
			limpa()
			u.aguarde(200)
		}

		retorne numero_sorteado
	}

	funcao combate(logico comece, logico boss){

		cadeia nome_inimigo
		cadeia pid_inimigo
		cadeia escape
		se(boss){
			escape = "ERR0&$%!"			
		}senao{
			escape = "./escape"
		}
		
		se(boss e comece){
			integridade_inimigo = 150
			dano_inimigo = 20
			bits_inimigo = integridade_inimigo
			dano = 4
			habilidade_inimigo = "I AM THE BOSS"
			
		}senao se(comece){
			sorteio_inimigo()
			dano = 1.5
		}
		cadeia danotxt = "│ DANO: " + dano +  "p/un"
		
		inteiro numero_quadrados_jogador = (bits * 10 / integridade)
		inteiro numero_quadrados_inimigo = (bits_inimigo * 10 / integridade_inimigo)
		se(numero_quadrados_jogador > 10){
			numero_quadrados_jogador = 10
		}senao se(numero_quadrados_jogador < 0){
			numero_quadrados_jogador = 0
		}
		
		cadeia escolha_
		u.aguarde(u.sorteia(300, 700))

		limpa()
		
		escreva("┌─────────────────────────────────────────────────────────────┐\n")
		escreva("│ Terminal Quest — Combat Session                             │\n")
		escreva("├─────────────────────────────────────────────────────────────┤\n")
		escreva("│ TARGET : ")
		se(nao boss){
			nome_inimigo = "daemon_corrompido"
			pid_inimigo = "0347"
		}senao{
			nome_inimigo = "vírus"
			pid_inimigo = "0000"
		}
		escreva(nome_inimigo)
		escreve_espacos(51 , nome_inimigo, "")
		escreva("│\n")
		escreva("│ PID: ", pid_inimigo)
		escreve_espacos(55 , pid_inimigo, "")
		escreva("│\n")
		escreva("│ STATUS : HOSTIL                                             │\n")
		escreva("│ ATAQUES : ", habilidade_inimigo)
		escreve_espacos(50 , habilidade_inimigo, "")
		escreva("│\n")
		escreva("└─────────────────────────────────────────────────────────────┘\n\n")

		u.aguarde(u.sorteia(300, 700))
		
		escreva("┌─ USER STATUS ───────────────────────────────────────────────┐\n")
		escreva("│ INTEGRIDADE   [")

		para(inteiro i = 0; i < numero_quadrados_jogador; i++){
			escreva("█")
		}
		para(inteiro i = 0; i < 10 - numero_quadrados_jogador; i++){
			escreva("░")
		}

		cadeia integridade_texto = integridade + ""
		cadeia bits_texto = bits + ""
		cadeia integridade_inimigo_texto = integridade_inimigo + ""
		cadeia bits_inimigo_texto = bits_inimigo + ""
		
		escreva("]  ", bits, "/", integridade, " BITS")
		escreve_espacos(26, bits_texto, integridade_texto)
		escreva("│\n")
		escreva("│ KERNEL V.", versao,  "                                                │\n")
		escreva("│ ARMA: 0 & 1                                                 │\n")
		escreva("│ DANO: ", dano, " p/un                                              │\n")
		escreva("└─────────────────────────────────────────────────────────────┘\n\n")

		u.aguarde(u.sorteia(300, 700))
		
		escreva("┌─ ENEMY STATUS ──────────────────────────────────────────────┐\n")
		escreva("│ INTEGRIDADE [")

		para(inteiro i = 0; i < numero_quadrados_inimigo; i++){
			escreva("█")
		}
		para(inteiro i = 0; i < 10 - numero_quadrados_inimigo; i++){
			escreva("░")
		}

		escreva("]  ", bits_inimigo, "/", integridade_inimigo, " BITS")

		escreve_espacos(28, bits_inimigo_texto, integridade_inimigo_texto)
		
		escreva("│\n")
		escreva("│ BUFFER: instável                                            │\n")
		escreva("│ DANO: ", dano_inimigo, " (20% crítico)                                       │\n")
		escreva("└─────────────────────────────────────────────────────────────┘\n\n")

		u.aguarde(u.sorteia(300, 700))
		
		escreva("┌─ COMMANDS ──────────────────────────────────────────────────┐\n")
		escreva("│ ./attack    ./daemon                                        │\n")
		escreva("│       ", escape, "                                              │\n")
		escreva("└─────────────────────────────────────────────────────────────┘\n\n")
		
		escreva("operator@kernel:~/Arena?$: ./") leia(escolha_)
		
		se(escolha_ == "attack"){
			escolher_arma()
			se(arma == "1"){
				bits_inimigo -= dano * rodar_dado("jogador")
			}senao se(arma == "0"){
				dano += mat.arredondar(u.sorteia(2, 5) / 10.0, 2)
			}
			se(bits_inimigo <= 0){
				se(luta_chefe){
					chefeMorto = verdadeiro
					luta_chefe = falso
					questionePoupar()
				}senao{
					inimigoMorto[y][x] = verdadeiro
				}
			}senao{
				bits -= dano_inimigo + rodar_dado("inimigo") / 5
			}
			se(bits <= 0){
				bits = 0
				morreu()
				retorne
			}senao se(bits_inimigo > 0){
				combate(falso, boss)
			}
		
		}senao se(escolha_ == "daemon"){
			mostrar_inventario(falso)
		}senao se(escolha_ == "escape"){
			se(luta_chefe){
				combate(falso, boss)
			}senao{
				
				se(rodar_dado("jogador") >= 10){
					desenha_matriz()
				}senao{
					se(nao pular_dialogo){
						falas("Má sorte ein... haha", 70, "desconhecido")
						escreva("\n\n     PRESSIONE ENTER PARA CONTINUAR: ")
						leia(pular_dialogo)
					}
					combate(falso, boss)
				}
			}
		}senao{
			combate(falso, boss)
		}
	}

	funcao sorteio_inimigo(){

		inteiro numero_sorteado

		numero_sorteado = u.sorteia(0, 20)
		//inimigo bombado
		se(numero_sorteado >= 18){
			integridade_inimigo = 50
			bits_inimigo = integridade_inimigo
			valor_kernels = 100
			numero_sorteado = u.sorteia(1, 3)
			se(numero_sorteado == 1){
				habilidade_inimigo = "STACK OVERFLOW"
				dano_inimigo = 18
			}senao se(numero_sorteado == 2){
				habilidade_inimigo = "KERNEL PANIC"
				dano_inimigo = 10
			}senao{
				habilidade_inimigo = "ROOTKIT"
				dano_inimigo = 18
			}
		}senao se(numero_sorteado >= 15){
			valor_kernels = 50
			integridade_inimigo = 40
			bits_inimigo = integridade_inimigo
			habilidade_inimigo = "THREAD SPLIT"
			dano_inimigo = 11
		}senao se(numero_sorteado >= 10){
			valor_kernels = 35
			integridade_inimigo = 30
			bits_inimigo = integridade_inimigo
			habilidade_inimigo= "CACHE STRIKE"
			dano_inimigo = 9
		}senao se(numero_sorteado >= 1){
			valor_kernels = 25 
			integridade_inimigo = 20
			bits_inimigo = integridade_inimigo
			habilidade_inimigo = "SCAN"
			dano_inimigo = 7
		}senao{
			valor_kernels = 10
			integridade_inimigo = 20
			bits_inimigo = integridade_inimigo
			habilidade_inimigo = "PING"
			dano_inimigo = 3
		}
	}

	funcao escolher_arma(){
		logico escolheu_certo = falso
		cadeia passar_dialogo
		arma = "0"
		faca{
			limpa()
			escreva("┌─────────────────────────────────────────────────────────────┐\n")
			escreva("│                    ESCOLHA SUA ARMA                         │\n")
			escreva("├─────────────────────────────────────────────────────────────┤\n")
			escreva("│                                                             │\n")
			escreva("│  [0] OVERCLOCK                                              │\n")
			escreva("│      Aumenta seus atributos de ataque.                      │\n")
			escreva("│                                                             │\n")
			escreva("│  [1] LÂMINA DE PROCESSO                                     │\n")
			escreva("│      Permite atacar o inimigo diretamente.                  │\n")
			escreva("│                                                             │\n")
			escreva("└─────────────────────────────────────────────────────────────┘\n")
			escreva("   ESCOLHA SUA ARMA (0/1):")
			leia(arma)
			
			se(arma != "0" e arma != "1"){
				limpa()
				para(inteiro i = 0; i < 3; i++){
				limpa()
				u.aguarde(500)
				escreva("┌─────────────────────────────────────────────────────────────┐\n")
				escreva("│                          ERRO                               │\n")
				escreva("└─────────────────────────────────────────────────────────────┘\n")
				u.aguarde(500)
			}
				
				escreva("│ Opção de arma inválida.                                     │\n")
				escreva("│ Escolha apenas [0] ou [1].                                  │\n")
				escreva("│                                                             │\n")
				escreva("│ Pressione ENTER para continuar.                             │\n")
				escreva("└─────────────────────────────────────────────────────────────┘\n")
				leia(passar_dialogo)
			}senao{
				escolheu_certo = verdadeiro
			}
		}enquanto(nao escolheu_certo)
	}

	funcao chameChefe(){
		para(inteiro k = 1; k <= 6; k++){

			//define_matriz
			para(inteiro i = 0; i < 8; i++){
				para(inteiro j = 0; j < 12; j++){
					
					
					se(k == 1){
					//borda
						se(j == 9 e i == 0){
							matriz[i][j] = "_"
						}senao se(j == 11 ou j == 0){
							matriz[i][j] = "|"
						}senao se(i == 7 ou i == 0){
							se(i ==  9){
								matriz[i][j] = "&"
							}senao{
							matriz[i][j] = "—"
							}
						}
						//jogador
						senao se(y == i e x == j){
							matriz[i][j] = "#"
						}senao{
							matriz[i][j] = "."
						}
					}

					senao se(k == 2 ou k == 5){
					//borda
						se(j == 9 e i == 0){
							matriz[i][j] = "."
						}senao se((i == 1 e ( j == 10 ou j == 8)) ou i == 2 e j == 9){
							 matriz[i][j] = "*"
						}
						senao se(j == 11 ou j == 0){
							matriz[i][j] = "|"
						}senao se(i == 7 ou i == 0){
							matriz[i][j] = "—"
							
						}
						//jogador
						senao se(y == i e x == j){
							matriz[i][j] = "#"
						}senao{
							matriz[i][j] = "."
						}
					}

					senao se(k == 3){
					//borda
					
						se(j == 9 e i == 0){
							matriz[i][j] = "?"
						}senao se((i == 1 e ( j == 10 ou j == 8)) ou i == 2 e j == 9){
							 matriz[i][j] = "*"
						}
						senao se(j == 11 ou j == 0){
							matriz[i][j] = "|"
						}senao se(i == 7 ou i == 0){
							se(i ==  9){
								matriz[i][j] = "&"
							}senao{
							matriz[i][j] = "—"
							}
						}
						//jogador
						senao se(y == i e x == j){
							matriz[i][j] = "#"
						}senao{
							matriz[i][j] = "."
						}
					}
					senao se(k == 4){
					//borda
					
						se(j == 9 e i == 0){
							matriz[i][j] = "."
						}senao se(j == 9 e i == 1){
							matriz[i][j] = "?"
						}senao se((i == 1 e ( j == 10 ou j == 8)) ou i == 2 e j == 9){
							 matriz[i][j] = "*"
						}
						senao se(j == 11 ou j == 0){
							matriz[i][j] = "|"
						}senao se(i == 7 ou i == 0){
							matriz[i][j] = "—"
						}
						//jogador
						senao se(y == i e x == j){
							matriz[i][j] = "#"
						}senao{
							matriz[i][j] = "."
						}
					}
					senao se(k == 6){
					//borda
					
						se(j == 9 e i == 0){
							matriz[i][j] = "."
							
						}senao se(j == 9 e i == 4){
							matriz[i][j] = "✵"
							
						}senao se((i == 1 e ( j == 10 ou j == 8)) ou i == 2 e j == 9){
							 matriz[i][j] = "*"
							 
						}senao se(j == 11 ou j == 0){
							matriz[i][j] = "|"
							
						}senao se(i == 7 ou i == 0){
							matriz[i][j] = "—"
						}
						//jogador
						senao se(y == i e x == j){
							matriz[i][j] = "#"
						}senao{
							matriz[i][j] = "."
						}
					}
				}
			}
				se(k == 1){
					u.aguarde(800)
					entrada_chefe(1, 400)
					entrada_chefe(2, 800)
				}
				senao se(k == 2){
					entrada_chefe(1, 400)
					entrada_chefe(3, 800)
					entrada_chefe(1, 400)
					entrada_chefe(4, 800)
					entrada_chefe(5, 800)
				}
				desenha_matriz()
				se(k == 2 ou k == 3){
					u.aguarde(1800)
				}senao se(k == 6){
					u.aguarde(800)
					falasChefe(verdadeiro)
				}senao{
					u.aguarde(800)
				}
		}
	}

	funcao entrada_chefe(inteiro sprite, real tempoSprite){
		se(sprite == 1){
		
			limpa()
		escreva("█▓▓    ░▓█████████████████████████████▓   ▓███████████▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓████████░      ▒▓▓▓█████\n")
		escreva("██████▓▒   ██████████████████████████████▓   █████████▓▓▓▓▓▒▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓██▓███░     ▒▓▓▓▓███████\n")
		escreva("████████████ ▓██████████████████████████████ ▓████████▓▓▓▓▓░▒▓▓▓▓▓▓ ▓▓▓▓████▓▒         ▒▓▓▓██████████\n")
		escreva("███████████████▓▓ ▓██████████▒░█▓███████████  ████████▓░▓▓ ░▓▓▓▓▓▓ ▓▓▓▓█▓▓▓  ░▒▓▓▓▓▓▓▓▓▓█████████████\n")
		escreva("████████████████████▒░▓████████▓  ▒██████████▓ ███████▓▓▒▓  ▓▓▓▓ ▒▓▓▓▓▓▓▓  ▒▓▓▓▓█████████████████████\n")
		escreva("██████████████████████████████████▓     ██████   ▒    ▓░   ▓▒▓ ▓▓▓▓▓▓▓▓   ▓▓▓▓▓▓▓▓▓▓██▓▓█████████████\n")
		escreva("████████████████████████████████████████  ██▒ █████████▒▓      ▓▓▓▓▓▓   ░▓▓▓▓▓▓▓▓▓▓▓▓▓██████████████▓\n")
		escreva("  ▓▓▓▓███████████████████████████████████▓░  ███▓ ███▓█████▓   ▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓████▓▓▓▓   ▓▓▓▓\n")
		escreva("                ▒▓████████████████████▓▒▒██  ████ ███▒▓███▒███     ▓▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓▓█▓▓▓████████\n")
		escreva("                     ░████████████████████▓  ███ ▒███ ▓██ ▒███░  ░▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("                        ░██████████▒  ▓██▒ ░░█░   ▒█▓ ███ ██▓    ▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("█████████████████████▓             ░▓█    ▒████ ██░  ▓██ ▒██ █      ░▓▓▓▓▒▒▓▓▓██▓▓▓▓█████████████████\n")
		escreva("█████████████████████████▓ ▒█████████████ ▒███▓▓   ▓        █    ░▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("██████████████████████████████████████████▒  ▓▓██████ ██████    ░   ░▒▓▓▓▓▓▓▓▓▓▓▓▓▓█▓████████████████\n")
		escreva("████████████████████████████████████  ▓██▓███▓   ▓▓███▓▓       ░▒▒▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("███████████████████▓▓ ▒████████████████████▓█  █▓▓▒        ░   ░▒▒▓▓▓▓▓▓   ▓▓▓▓▓▓▓▓▓▒ ▒▓█████████████\n")
		escreva("████████████▓▓ ░▓▓▓██████████████████████▓▒  ░████████▒▒░ ▒▒▒▒   ▒▓▓▓▓▓▓▓▓▒                 ░█▓██████\n")
		escreva("███████▓▒    ▓▓▓█████████████████████▓▓   ▓▓▓█▓███████▒▒▒ ▒▒▒▒▒▓  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓          ▓▓█\n")
		escreva("█▒       ▓▓█████████████████████▓▓█▓██░ █████▓████▓███▓▓▒▒░░▒▒▒▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██▓█▓          \n")
		escreva("    ▓▓▓▓▓████████████████████████████▓  █████████▓████▓▓▓▒ ▒▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓████████▓      \n")
		escreva("▓▓▓▓█████████████████████████████████  ▓████████▓█████▓▓▓▓▓░▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓███████████▓▒  ")
		u.aguarde(tempoSprite / 1.5)
		
		}
		
		senao se(sprite == 2){
			limpa()
		escreva("█▓▓    ░▓█████████████████████████████▓   ▓███████████▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓████████░      ▒▓▓▓█████\n")
		escreva("██████▓▒   ██████████████████████████████▓   █████████▓▓▓▓▓▒▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓██▓███░     ▒▓▓▓▓███████\n")
		escreva("████████████ ▓██████████████████████████████ ▓████████▓▓▓▓▓░▒▓▓▓▓▓▓ ▓▓▓▓████▓▒         ▒▓▓▓██████████\n")
		escreva("███████████████▓▓ ▓██████████▒░█▓███████████  ████████▓░▓▓ ░▓▓▓▓▓▓ ▓▓▓▓█▓▓▓  ░▒▓▓▓▓▓▓▓▓▓█████████████\n")
		escreva("████████████████████▒░▓████████▓  ▒██████████▓ ███████▓▓▒▓  ▓▓▓▓ ▒▓▓▓▓▓▓▓  ▒▓▓▓▓█████████████████████\n")
		escreva("██████████████████████████████████▓     ██████   ▒    ▓░   ▓▒▓ ▓▓▓▓▓▓▓▓   ▓▓▓▓▓▓▓▓▓▓██▓▓█████████████\n")
		escreva("████████████████████████████████████████  ██▒                  ▓▓▓▓▓▓   ░▓▓▓▓▓▓▓▓▓▓▓▓▓██████████████▓\n")
		escreva("  ▓▓▓▓███████████████████████████████████▓░                    ▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓████▓▓▓▓   ▓▓▓▓\n")
		escreva("                ▒▓████████████████████▓▒▒██                        ▓▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓▓█▓▓▓████████\n")
		escreva("                     ░████████████████████▓                      ░▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("                        ░██████████▒  ▓██▒                       ▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("█████████████████████▓             ░▓█                              ░▓▓▓▓▒▒▓▓▓██▓▓▓▓█████████████████\n")
		escreva("█████████████████████████▓ ▒█████████████                        ░▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("██████████████████████████████████████████▒                     ░   ░▒▓▓▓▓▓▓▓▓▓▓▓▓▓█▓████████████████\n")
		escreva("████████████████████████████████████  ▓██▓███▓                 ░▒▒▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("███████████████████▓▓ ▒████████████████████▓█  █▓▓▒        ░   ░▒▒▓▓▓▓▓▓   ▓▓▓▓▓▓▓▓▓▒ ▒▓█████████████\n")
		escreva("████████████▓▓ ░▓▓▓██████████████████████▓▒  ░████████▒▒░ ▒▒▒▒   ▒▓▓▓▓▓▓▓▓▒                 ░█▓██████\n")
		escreva("███████▓▒    ▓▓▓█████████████████████▓▓   ▓▓▓█▓███████▒▒▒ ▒▒▒▒▒▓  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓          ▓▓█\n")
		escreva("█▒       ▓▓█████████████████████▓▓█▓██░ █████▓████▓███▓▓▒▒░░▒▒▒▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██▓█▓          \n")
		escreva("    ▓▓▓▓▓████████████████████████████▓  █████████▓████▓▓▓▒ ▒▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓████████▓      \n")
		escreva("▓▓▓▓█████████████████████████████████  ▓████████▓█████▓▓▓▓▓░▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓███████████▓▒  ")
		u.aguarde(tempoSprite)
		
		}
		
		senao se(sprite == 3){
			limpa()
		escreva("█▓▓    ░▓█████████████████████████████▓   ▓███████████▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓████████░      ▒▓▓▓█████\n")
		escreva("██████▓▒   ███████████████████████████▓▒▒    ███▓▒▒███▓▓▓▓  ▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓██▓███░     ▒▓▓▓▓███████\n")
		escreva("████████████ ▓███████████████████████████           ██▓▓▓   ▒▓▓▓▓▓▓ ▓▓▓▓████▓▒         ▒▓▓▓██████████\n")
		escreva("███████████████▓▓ ▓██████████▒░█▓███▓▒▒                             ▓▓▓█▓▓▓  ░▒▓▓▓▓▓▓▓▓▓█████████████\n")
		escreva("████████████████████▒░▓████████▓  ▒██                                      ▒▓▓▓▓█████████████████████\n")
		escreva("██████████████████████████████████▓                                       ▓▓▓▓▓▓▓▓▓▓██▓▓█████████████\n")
		escreva("█████████████████████████████████████                                   ░▓▓▓▓▓▓▓▓▓▓▓▓▓██████████████▓\n")
		escreva("  ▓▓▓▓██████████████████████████████                                  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓████▓▓▓▓   ▓▓▓▓\n")
		escreva("                ▒▓████████████████████▓▒▒                              ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓▓█▓▓▓████████\n")
		escreva("                     ░██████████████▓▒▒                                 ▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("                        ░██████████▒  ▓██▒                                  ▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("█████████████████████▓             ░▓█                                   ▒▒▓▓▓██▓▓▓▓█████████████████\n")
		escreva("█████████████████████████▓ ▒████████                                        ▓▓▓▓▓▓▓██████████████████\n")
		escreva("██████████████████████████████████████▓▒▒ ▒                            ▓▓▓▓▓▓▓▓▓▓▓▓█▓████████████████\n")
		escreva("████████████████████████████████████  ▓██▓███▓                           ▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("███████████████████▓▓ ▒████████████████████▓█                              ▓▓▓▓▓▓▓▓▓▒ ▒▓█████████████\n")
		escreva("████████████▓▓ ░▓▓▓██████████████████████▓▒  ░███         ▒▒▒▒   ▒▓▓▓▓▓▓▓▓▒                 ░█▓██████\n")
		escreva("███████▓▒    ▓▓▓█████████████████████▓▓   ▓▓▓█▓███████▒▒▒ ▒▒▒▒▒▓  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓          ▓▓█\n")
		escreva("█▒       ▓▓█████████████████████▓▓█▓██░ █████▓████▓███▓▓▒▒░░▒▒▒▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██▓█▓          \n")
		escreva("    ▓▓▓▓▓████████████████████████████▓  █████████▓████▓▓▓▒ ▒▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓████████▓      \n")
		escreva("▓▓▓▓█████████████████████████████████  ▓████████▓█████▓▓▓▓▓░▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓███████████▓▒  ")
		u.aguarde(tempoSprite)
		}
		
		senao se(sprite == 4){
			limpa()
		escreva("█▓▓    ░▓█████████████████████████████▓   ▓███████████▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓████████░      ▒▓▓▓█████\n")
		escreva("██████▓▒   ███████████████████████████▓▒▒    ███▓▒▒███      ▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓██▓███░     ▒▓▓▓▓███████\n")
		escreva("████████████ ▓███████████████████████████                   ▒▓▓▓▓▓▓ ▓▓▓▓████▓▒         ▒▓▓▓██████████\n")
		escreva("███████████████▓▓ ▓██████████▒░█▓███▓▒▒                                      ░▒▓▓▓▓▓▓▓▓▓█████████████\n")
		escreva("████████████████████▒░▓████████▓                                           ▒▓▓▓▓█████████████████████\n")
		escreva("███████████████████████████████                                              ▓▓▓▓▓▓▓██▓▓█████████████\n")
		escreva("████████████████████████████████                                             ▓▓▓▓▓▓▓▓▓██████████████▓\n")
		escreva("  ▓▓▓▓███████████████████████████                                             ▓▓▓▓▓▓▓▓████▓▓▓▓   ▓▓▓▓\n")
		escreva("                ▒▓████████████████                                         ▓▓▓▓▓▓▓▓▓▓▓█▓▓█▓▓▓████████\n")
		escreva("                     ░████████████                                      ▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("                        ░████████                                           ▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("█████████████████████▓                                                   ▒▒▓▓▓██▓▓▓▓█████████████████\n")
		escreva("█████████████████████████▓ ▒████████                                        ▓▓▓▓▓▓▓██████████████████\n")
		escreva("█████████████████████████████████                                      ▓▓▓▓▓▓▓▓▓▓▓▓█▓████████████████\n")
		escreva("█████████████████████████████████                                        ▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("███████████████████▓▓ ▒███████████                                         ▓▓▓▓▓▓▓▓▓▒ ▒▓█████████████\n")
		escreva("████████████▓▓ ░▓▓▓███████████████                               ▒▓▓▓▓▓▓▓▓▒                 ░█▓██████\n")
		escreva("███████▓▒    ▓▓▓█████████████████████▓▓   ▓▓▓█▓█               ▓  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓          ▓▓█\n")
		escreva("█▒       ▓▓█████████████████████▓▓█▓██░ █████▓████▓███▓▓▒▒░░▒▒▒▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██▓█▓          \n")
		escreva("    ▓▓▓▓▓████████████████████████████▓  █████████▓████▓▓▓▒ ▒▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓████████▓      \n")
		escreva("▓▓▓▓█████████████████████████████████  ▓████████▓█████▓▓▓▓▓░▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓███████████▓▒  ")
		u.aguarde(tempoSprite)
		}
		
		senao se(sprite == 5){
			limpa()
		escreva("█▓▓    ░▓█████████████████████████████▓   ▓███████████▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓████████░      ▒▓▓▓█████\n")
		escreva("██████▓▒   ███████████████████████████▓▒▒    ███▓▒▒███      ▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓██▓███░     ▒▓▓▓▓███████\n")
		escreva("████████████ ▓███████████████████████████                   ▒▓▓▓▓▓▓ ▓▓▓▓████▓▒         ▒▓▓▓██████████\n")
		escreva("███████████████▓▓ ▓██████████▒░█▓███▓▒▒           ▒▓▓░   ▓▒░░▓▓▓▒░           ░▒▓▓▓▓▓▓▓▓▓█████████████\n")
		escreva("████████████████████▒░▓████████▓     ▒█░  ░▓▒░▒████████▓▓██▓▒              ▒▓▓▓▓█████████████████████\n")
		escreva("███████████████████████████████    ▒▒▓░     ▓▓▓▓▓▓▓▓▓▓██▓▓▓▓█▓ ▓▓   ▒▓███▒   ▓▓▓▓▓▓▓██▓▓█████████████\n")
		escreva("████████████████████████████████      ░▒▓▓▓██▓▓▓▓▓▓▓▓▓▓█▓▓▓▓▓██▒░▒▒▒▓▓░ ░▓   ▓▓▓▓▓▓▓▓▓██████████████▓\n")
		escreva("  ▓▓▓▓███████████████████████████        ░██▓▓▓▓▓▓▓▓▓▒▒▒▓▓▓▓▓▓███▒            ▓▓▓▓▓▓▓▓████▓▓▓▓   ▓▓▓▓\n")
		escreva("                ▒▓████████████████     ░ ▒██▓▓▓▓▓▒▒▒▒▒▒▒▒▓▓▓▓▓▓██░         ▓▓▓▓▓▓▓▓▓▓▓█▓▓█▓▓▓████████\n")
		escreva("                     ░████████████    ▓▓▓▓▓▓▓▒▓▓▓▒▒▒▒▒▒▒▒▒▓▓▓▓▓▓█▓▒░▒▓░ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("                        ░████████    ▓▓▓▓███▓▓▓▓▓▒▒▒▒▒▒▒▒▒▓▓▓▓▓██▒░▒░▓░     ▓▓▓▓▓▓▓▓▓▓███████████████\n")
		escreva("█████████████████████▓                   ███▓▓▓▓▓▒▒▒▒▒▒▒▓▓▓▓▓▓███░░▓░    ▒▒▓▓▓██▓▓▓▓█████████████████\n")
		escreva("█████████████████████████▓ ▒████████     ▓██▓▓▓▓▓▓▓▒▒▒▓▓▓▓▓▓▓▓██▒           ▓▓▓▓▓▓▓██████████████████\n")
		escreva("█████████████████████████████████     ░▓▓▓▓██▓▓▓▓▓▓▓▓▓▓▓█▓▓████▓▒░     ▓▓▓▓▓▓▓▓▓▓▓▓█▓████████████████\n")
		escreva("█████████████████████████████████ ▓█▓▓▓     ██▓▓▓▓▓▓▓▓▓█▓▓█   ▒▒░        ▓▓▓▓▓▓▓▓▓▓██████████████████\n")
		escreva("███████████████████▓▓ ▒███████████           ███▓██  ██▓█▓▓▓█              ▓▓▓▓▓▓▓▓▓▒ ▒▓█████████████\n")
		escreva("████████████▓▓ ░▓▓▓███████████████             █▓  █▓      ▓▓█   ▒▓▓▓▓▓▓▓▓▒                 ░█▓██████\n")
		escreva("███████▓▒    ▓▓▓█████████████████████▓▓   ▓▓▓█▓█               ▓  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓█▓          ▓▓█\n")
		escreva("█▒       ▓▓█████████████████████▓▓█▓██░ █████▓████▓███▓▓▒▒░░▒▒▒▓▓▒  ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓██▓█▓          \n")
		escreva("    ▓▓▓▓▓████████████████████████████▓  █████████▓████▓▓▓▒ ▒▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓████████▓      \n")
		escreva("▓▓▓▓█████████████████████████████████  ▓████████▓█████▓▓▓▓▓░▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓███████████▓▒  ")
		}
		u.aguarde(tempoSprite)
	}

	funcao falasChefe(logico comeco){
		
		se(comeco){
			
			falas("Hmm... O que temos aqui?",76, "virus")
			falas("Q-Quem é você?", 76, "jogador")
			falas("Eu? HAHA, eu sou um virus, mais especificamente o que te colocou aqui.",76, "virus")
			falas("Além disso sou eu quem vai te impedir de continuar.", 76, "virus")
			falas("Você vai ter que lutar, ele tem poderes sobre o jogo, não vai te deixar fugir.",76, "daemon")
			falas("Tudo bem, Vem pra cima.", 76, "jogador")
		}senao{
			
			falas("Você vai me poupar?",76, "virus")
			falas("Não ganho nada te matando",76, "jogador")
			falas("Muito Obrigado, vou te mandar de volta.",76, "virus")
		}
	}

	funcao escreve_espacos(inteiro largura, cadeia texto1, cadeia texto2){
	
		inteiro n_espacos = largura - txt.numero_caracteres(texto1) - txt.numero_caracteres(texto2)

		para(inteiro i = 0; i < n_espacos; i++){
				escreva(" ")
		}
	}

	funcao morreu(){
		limpa()
		escreva("        .--. \n")
		escreva("       |o_o | \n")
		escreva("       |:_/ | \n")
		escreva("      //   \\ \\ \n")
		escreva("     (|     | ) \n")
		escreva("    /'\\_   _/`\\ \n")
		escreva("    \\___)=(___/ \n")
		escreva("\n")
		
		escreva("        KERNEL PANIC - SYSTEM HALTED\n")
		escreva("\n")
		
		escreva("O sistema encontrou um erro critico e nao pode continuar.\n")
		escreva("\n")
		
		escreva("Erro: processo do jogador encerrado\n")
		escreva("\n")
		
		escreva("Player:      TERMINADO\n")
		escreva("Integridade: 0%%\n")
		escreva("Bits:        00000000\n")
		escreva("\n")
		
		escreva("Falha no nucleo do sistema.\n")
		escreva("Memoria do processo perdida.\n")
		escreva("\n")
		
		escreva("              SYSTEM FAILURE\n")
		parou_por_algum_motivo = verdadeiro
	}

	funcao questionePoupar(){
		falas("Espere, se me poupar eu consigo te enviar de volta para o mundo real.", 70, "virus")
		
		cadeia poupar
		escreva_lento("       Poupar?", 70)
		escreva("\n\n             ./yes     ./no \n\n\noperator@kernel:~$: ")
		leia(poupar)
		se(poupar == "./yes"){
			falasChefe(falso)
			finalReal()
			parou_por_algum_motivo = verdadeiro
		}senao{
			escreva("Você decide finalizar o Virus.")
			finalFalso()
			parou_por_algum_motivo = verdadeiro
		}
	}

	funcao finalReal(){
		limpa()
		escreva_lento("\n\nVocê acorda em um ciber café,\n\nvocê está todo babado, e acredita que estava sonhando...", 90)
		u.aguarde(3000)
		escreva_lento("\n\n\nSe não fosse pela mensagem no seu neuralink:", 90)
		escreva_lento("\n\nVirus encontrado e destrui- Você sabe demais,", 70)
		escreva_lento(" neuralink se autodestruindo em 3...", 110)
	}
	
	funcao finalFalso(){
		limpa()
		escreva_lento("Você continua vagando e percebe que não há possibilidade de fuga,\nagora você começa uma vida nesse mundo, somente com batalhas, novas salas e chaves\n\n\n\n", 70)
		escreva_lento("Será que aquele virus falava a verdade?", 70)
	}
}
